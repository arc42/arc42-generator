import org.asciidoctor.gradle.jvm.AsciidoctorTask

// // gradle build script for the arc42 template
// ("plain" subproject, creating the template without help text)
//
// free software - without guarantee, use at your own risk
// ========================================================

// set subproject build directory
def localBuildDir = [:]

def language = '%LANG%'
def projectName = '%TYPE%'
//if (!project?.ext?.config) {
//    project.ext.config = new ConfigSlurper().parse file('buildconfig.groovy').text
//}
logger.info "*"*80
logger.info project.name
logger.info globalBuildDir

config.formats.each { format, params ->
    localBuildDir[format] = globalBuildDir + '/'+language+'/'+format+'/' + projectName
    new File(localBuildDir[format]).mkdirs()
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}
apply plugin: "org.asciidoctor.jvm.convert"

// common settings
tasks.withType(AsciidoctorTask) { docTask ->
    sourceDir = project.file('./src')

    attributes(
            toc         : 'left',
            doctype     : 'book',
            icons       : 'font',
            sectlink    : true,
            sectanchors : true,
            numbered    : true,
            revnumber   : '%REVNUMBER%',
            revdate     : '%REVDATE%',
            revremark   : '%REVREMARK%',

            imagesdir   : 'images',
    )
    logDocuments true
}

task copyAsciidoc (
        description: 'asciidoc is the master, but we copy it to the build directory, too'
) {
    doLast {
        copy {
            from project.file('./src/.')
            exclude('arc42-template.adoc')
            into localBuildDir.asciidoc+"/src/."
        }
        copy {
            from project.file('./src/.')
            include('arc42-template.adoc')
            into localBuildDir.asciidoc
        }
    }
}
defaultTasks 'arc42'

task copyImages(
        description: 'copy images into build output folder so that references can be resolved '
) {
    localBuildDir.each { format, dir ->
        if (config.formats[format].imageFolder) {
            def taskName = "copyImagesFor_" + projectName + "_" + language + "_" + format
            tasks.create(name: taskName, type: Copy) {
                def outImgs = (format == 'mkdocs' ? '/docs/images' : '/images')
                logger.info ">>>> copyImages from " + file('images')
                logger.info ">>>>              to " + file(dir+outImgs)
                inputs.dir file('images')
                outputs.dir file(dir + outImgs)
                from file('images')
                into file(dir + outImgs)
                doLast {}
            }
            tasks.copyImages.dependsOn << taskName
        }
    }
}

task generateHTML (
    type: AsciidoctorTask,
    dependsOn: [copyImages, copyAsciidoc],
    description: 'use html5 as asciidoc backend') {
    sources {
        file("src")
        include 'arc42-template.adoc'
    }
    outputDir = new File( localBuildDir.html )
    outputOptions {
        backends "html5"
    }
}

task generateDocbook (
        type: AsciidoctorTask,
        dependsOn: [copyImages, copyAsciidoc],
        description: 'use docbook as asciidoc backend') {
    sources {
        file("src")
        include 'arc42-template.adoc'
    }
    if (localBuildDir.docbook) {
        outputDir = new File(localBuildDir.docbook)
        outputOptions {
            backends 'docbook'
        }
    } else {
        println """
    Failed to generate DocBook
    it seems that a format which needs DocBook as intermediate format is configured in buildconfig.groovy
    please add
        'docbook': [imageFolder: true]
    to the list of formats
"""
        //end with exception :-)
        new File(localBuildDir.docbook)
    }
}
//Multipage-Version
task generateDocbookMP (
        type: AsciidoctorTask,
        dependsOn: [copyImages, copyAsciidoc],
        description: 'use docbook as asciidoc backend') {
    if (localBuildDir.docbook) {
        outputDir = new File(localBuildDir.docbook+'MP')
        outputOptions {
            backends 'docbook'
        }
    } else {
        println """
    Failed to generate markdownMP
    it seems that a format which needs DocBook as intermediate format is configured in buildconfig.groovy
    please add
        'docbook': [imageFolder: true]
    to the list of formats
"""
        //end with exception :-)
        new File(localBuildDir.docbook)
    }
}

task convert2Latex (
        dependsOn: [copyImages, generateDocbook ],
        type: Exec
) {
    executable = "pandoc"
    args = ['-r','docbook',
        '-s',
        '-t','latex',
        '-o',localBuildDir.latex+'/arc42-template-'+language+'.tex',
        localBuildDir.docbook+'/arc42-template.xml']
    if (language=='RU') {
        args += ['-V','fontenc=T1,T2A']
    } 
    System.out.println("*"*80)
    System.out.println args
    System.out.println("*"*80)
    doLast {
        def file = new File(localBuildDir.latex+'/arc42-template-'+language+'.tex')
        file.write(file.text.replaceAll("\u2009", " "))
    }
}
task convert2Docx (
        dependsOn: [copyImages, generateDocbook ],
        type: Exec
) {
    executable = "pandoc"
    args = ['-r','docbook',
            '-t','docx',
            '-o',localBuildDir.docx+'/arc42-template-'+language+'.docx',
            localBuildDir.docbook+'/arc42-template.xml']
}
task convert2Epub (
    dependsOn: [copyImages, generateDocbook ],
    type: Exec
) {
    //commandLine "pandoc -r arc42-template.xml -o arc42-template.docx "
    executable = "pandoc"
    args = ['-r','docbook',
            '-t','epub',
            '-o',localBuildDir.epub+'/arc42-template-'+language+'.epub',
        localBuildDir.docbook+'/arc42-template.xml']
}
task convert2Rst (
    dependsOn: [copyImages, generateDocbook ],
    type: Exec
) {
    //commandLine "pandoc -r arc42-template.xml -o arc42-template.docx "
    executable = "pandoc"
    args = ['-r','docbook',
            '-t','rst',
            '-o',localBuildDir.rst+'/arc42-template-'+language+'.rst',
        localBuildDir.docbook+'/arc42-template.xml']
}
task convert2Markdown (
    dependsOn: [copyImages, generateDocbook ],
    type: Exec
) {
    //commandLine "pandoc -r arc42-template.xml -o arc42-template.docx "
    executable = "pandoc"
    args = ['-r','docbook',
            '-t','markdown',
            '-o',localBuildDir.markdown+'/arc42-template-'+language+'.md',
        localBuildDir.docbook+'/arc42-template.xml']
}
task convert2MarkdownStrict (
    dependsOn: [copyImages, generateDocbook ],
    type: Exec
) {
    //commandLine "pandoc -r arc42-template.xml -o arc42-template.docx "
    executable = "pandoc"
    args = ['-r','docbook',
            '-t','markdown_strict',
            '-o',localBuildDir.markdownStrict+'/arc42-template-'+language+'.md',
        localBuildDir.docbook+'/arc42-template.xml']
    System.out.println ">>>>>>>>> ${localBuildDir.markdownStrict}"
}
task convert2MarkdownMPStrict (
        dependsOn: [copyImages, generateDocbookMP ]
) {
    def sourceFolder = file(localBuildDir.docbook + 'MP/.')
    if (sourceFolder.exists()) {
        sourceFolder.eachFile { myFile ->
            if (myFile.name.endsWith('.xml')) {
                def taskName = 'convert2MarkdownMPStrict_file_' + myFile.name-'.xml'
                new File(localBuildDir.markdownMPStrict + '/').mkdirs()
                def theTask = tasks.create(name: taskName, type: Exec, dependsOn: [copyImages, generateDocbookMP]) {
                    //commandLine "pandoc -r arc42-template.xml -o arc42-template.docx "
                    executable = "pandoc"
                    args = ['-r', 'docbook',
                            '-t', 'markdown_strict',
                            '-o', localBuildDir.markdownMPStrict + '/' + myFile.name-'xml'+'md',
                            myFile.canonicalPath]
                }
                it.finalizedBy(theTask)
            }
        }
    }
}
task convert2MarkdownMP (
        dependsOn: [copyImages, generateDocbookMP ]
) {
    def sourceFolder = file(localBuildDir.docbook + 'MP/.')
    if (sourceFolder.exists()) {
        sourceFolder.eachFile { myFile ->
            if (myFile.name.endsWith('.xml')) {
                def taskName = 'convert2MarkdownMP_file' + myFile.name-'.xml'
                new File(localBuildDir.markdownMP + '/').mkdirs()
                def theTask = tasks.create(name: taskName, type: Exec, dependsOn: [copyImages, generateDocbookMP]) {
                    //commandLine "pandoc -r arc42-template.xml -o arc42-template.docx "
                    executable = "pandoc"
                    args = ['-r', 'docbook',
                            '-t', 'markdown',
                            '-o', localBuildDir.markdownMP + '/' + myFile.name-'xml'+'md',
                            myFile.canonicalPath]
                }
                it.finalizedBy(theTask)
            }
        }
    }
}
task convert2GitHubMarkdown (
    dependsOn: [copyImages, generateDocbook ],
    type: Exec
) {
    //commandLine "pandoc -r arc42-template.xml -o arc42-template.docx "
    executable = "pandoc"
    args = ['-r','docbook',
            '-t','gfm',
            '-o',localBuildDir.gitHubMarkdown+'/arc42-template-'+language+'.md',
        localBuildDir.docbook+'/arc42-template.xml']
}
task convert2GitHubMarkdownMP (
        dependsOn: [copyImages, generateDocbookMP ]
) {
    
    def sourceFolder = file(localBuildDir.docbook + 'MP/.')
    if (sourceFolder.exists()) {
        sourceFolder.eachFile { myFile ->
            if (myFile.name.endsWith('.xml')) {
                def taskName = 'convert2GitHubMarkdownMP_' + myFile.name - '.xml'
                new File(localBuildDir.gitHubmarkdownMP + '/').mkdirs()
                def theTask = tasks.create(name: taskName, type: Exec, dependsOn: [copyImages, generateDocbookMP]) {
                    executable = "pandoc"
                    args = ['-r', 'docbook',
                            '-t', 'gfm',
                            '-o', localBuildDir.gitHubMarkdownMP + '/' + myFile.name-'xml'+'md',
                            myFile.canonicalPath]
                }
                it.finalizedBy(theTask)
            }
        }
    }
    
}
task convert2Mkdocs (
    dependsOn: [copyImages, generateDocbook ],
    type: Exec
) {
    //commandLine "pandoc -r arc42-template.xml -o arc42-template.docx "
    new File(localBuildDir.mkdocs + '/docs/').mkdirs()
    new File(localBuildDir.mkdocs + '/mkdocs.yml')
        .write("""  |site_name: Arc42 Template
                    |site_description: Software architecture documentation with arc42
                    |
                    |markdown_extensions:
                    |  - extra
                    |  - admonition
                    |  - attr_list
                    |  - def_list
                    |  - footnotes
                    |  - meta
                    |  - toc:
                    |      permalink: true
                    |
                    |plugins:
                    |  - search
                    |
                    |theme:
                    |  name: material
                    |  features:
                    |    - navigation.instant
                    |    - navigation.tracking
                    |    - navigation.expand
                    |    - navigation.indexes
                    |    - toc.follow
                    |
                    |nav:
                    |  - index.md
                    |""".stripMargin())

    executable = "pandoc"
    args = [
        '-r','docbook',
        '-t','markdown_phpextra',
        '--toc',                   // Include table of contents
        '--wrap=none',             // No text wrapping
        '--reference-links',       // Use reference-style links
        '-o', localBuildDir.mkdocs+'/docs/index.md',
        localBuildDir.docbook+'/arc42-template.xml'
    ]

    doLast {
        def indexFile = new File(localBuildDir.mkdocs + '/docs/index.md')
        def indexContent = indexFile.collect { it.replace("# ", "## ") }.drop(1).join("\n") // remove first line, which is the title of the file
        indexFile.write(""" |---
                            |hide:
                            |   - navigation
                            |---
                            |
                            |# Arc42 Template
                            |
                            |${indexContent}""".stripMargin())
    }
}
task convert2MkdocsMP (
        dependsOn: [copyImages, generateDocbookMP ]
) {
    def sourceFolder = file(localBuildDir.docbook + 'MP/.')
    def lastFileTask = null
    def fileTasks = []
    if(sourceFolder.exists()) {
        sourceFolder.eachFile { myFile ->
            if (myFile.name.endsWith('.xml')) {
                def taskName = 'convert2MkdocsMP_file_' + myFile.name - '.xml'
                new File(localBuildDir.mkdocsMP + '/docs/').mkdirs()
                new File(localBuildDir.mkdocsMP + '/mkdocs.yml')
                    .write("""  |site_name: Arc42 Template
                                |site_description: Software architecture documentation with arc42
                                |
                                |markdown_extensions:
                                |  - extra
                                |  - admonition
                                |  - attr_list
                                |  - def_list
                                |  - footnotes
                                |  - meta
                                |  - toc:
                                |      permalink: true
                                |
                                |plugins:
                                |  - search
                                |
                                |theme:
                                |  name: material
                                |  features:
                                |    - navigation.instant
                                |    - navigation.tracking
                                |    - navigation.expand
                                |    - navigation.indexes
                                |    - toc.follow
                                |""".stripMargin())
                def outfile = myFile.name-'xml'+'md'
                if (myFile.name == "arc42-template.xml") {
                    outfile = 'index.md'
                }
                def theTask = tasks.create(name: taskName, type: Exec, dependsOn: [copyImages, generateDocbookMP]) {
                    executable = "pandoc"
                    args = ['-r', 'docbook',
                            '-t', 'markdown_phpextra',
                            '--toc',                   // Include table of contents
                            '--wrap=none',             // No text wrapping
                            '--reference-links',       // Use reference-style links
                            '-o', localBuildDir.mkdocsMP + '/docs/' + outfile,
                            myFile.canonicalPath]
                }
                lastFileTask = theTask
                it.finalizedBy(theTask)
                fileTasks.add(theTask)
            }
        }
        def thePostTask = tasks.create(name: "convert2Mkdocs_post", dependsOn: [copyImages, generateDocbookMP]) {
            doLast {
                new File(localBuildDir.mkdocsMP + '/docs/config.md').delete()
                new File(localBuildDir.mkdocsMP + '/docs/about-arc42.md').delete()
                def indexFile = new File(localBuildDir.mkdocsMP + '/docs/index.md')
                def indexHeader = indexFile.getText('UTF-8').split(">.").first().substring(1).trim() + ">.\n\n"
                indexFile.withWriter('UTF-8') { out ->
                    out.write(indexHeader)
                    new File(localBuildDir.mkdocsMP + "/docs/.").eachFile {
                        if (it.name != "index.md") {
                            def title = new File(localBuildDir.mkdocsMP + "/docs/" + it.name).withReader { it.readLine() }
                            if (title.startsWith("#")) {
                                title = title.split("[#{]+")[1].trim()
                                out.write("\n0. [${title}](${it.name})\n")
                            }
                        }
                    }
                }
            }
        }
        thePostTask.mustRunAfter(fileTasks)
        lastFileTask.finalizedBy(thePostTask)
    }
}

task convert2Textile (
        dependsOn: [copyImages, generateDocbookMP ]
) {
    def sourceFolder = file(localBuildDir.docbook + 'MP/.')
    if(sourceFolder.exists()) {
        sourceFolder.eachFile { myFile ->
            if (myFile.name.endsWith('.xml')) {
                def taskName = 'convert2Textile_file' + myFile.name-'.xml'
                new File(localBuildDir.textile + '/').mkdirs()
                def theTask = tasks.create(name: taskName, type: Exec, dependsOn: [copyImages, generateDocbookMP]) {
                    //commandLine "pandoc -r arc42-template.xml -o arc42-template.docx "
                    executable = "pandoc"
                    args = ['-r', 'docbook',
                            '-t', 'textile',
                            '-o', localBuildDir.textile + '/'+myFile.name-'xml'+'textile',
                            myFile.canonicalPath]
                }
                it.finalizedBy(theTask)
            }
        }
    }
}
task convert2Textile2 (
        dependsOn: [copyImages, generateDocbookMP ]
) {
    def sourceFolder = file(localBuildDir.docbook + 'MP/.')
    if(sourceFolder.exists()) {
        sourceFolder.eachFile { myFile ->
            if (myFile.name.endsWith('.xml')) {
                //rewrite file without <col elements
                def file = myFile.text
                def newFile = ""
                file.eachLine { line ->
                    if (line ==~/.*<colspec[^>]*>.*/) {
                        //ignore
                    } else {
                        newFile += line + "\n"
                    }
                }
                new File(myFile.canonicalPath+"2").write(newFile)
                def taskName = 'convert2Textile2_file_' + myFile.name-'.xml'
                new File(localBuildDir.textile2 + '/').mkdirs()
                //println rootDir.canonicalPath+'/width-filter-docBook.lua'
                def theTask = tasks.create(name: taskName, type: Exec, dependsOn: [copyImages, generateDocbookMP]) {
                    //commandLine "pandoc -r arc42-template.xml -o arc42-template.docx "
                    executable = "pandoc"
                    args = ['-r', 'docbook',
                            '-t', 'textile',
                            '-o', localBuildDir.textile2 + '/'+myFile.name-'xml'+'textile',
                            myFile.canonicalPath+'2']
                }
                it.finalizedBy(theTask)
            }
        }
    }
}
task export2Confluence (
    dependsOn: [generateHTML]
) {

}
task arc42(
    dependsOn: [copyImages, generateHTML, generateDocbook, copyAsciidoc, export2Confluence, convert2Latex, convert2Docx, convert2Epub, convert2Rst, convert2Markdown, convert2MarkdownStrict, convert2GitHubMarkdown, convert2Mkdocs, generateDocbookMP],
    description: 'collects all required tasks for creating arc42 single page documentation',
    group: 'arc42-template'
) {
    doLast {
        println "globalBuildDir = ${globalBuildDir}"
        println "localBuildDir = ${localBuildDir}"
    }
}
task arc42MP(
    dependsOn: [convert2MarkdownMP, convert2MarkdownMPStrict, convert2GitHubMarkdownMP, convert2MkdocsMP, convert2Textile, convert2Textile2],
    description: 'collects all required tasks for creating arc42 multipage documentation. Requires "generateDocbookMP" task to have been run beforehand!',
    group: 'arc42-template'
) {
    doLast {
        println "globalBuildDir = ${globalBuildDir}"
        println "localBuildDir = ${localBuildDir}"
    }
}

