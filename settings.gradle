//

def config = new ConfigSlurper().parse file('buildconfig.groovy').text

//let's fetch all generated subprojects
def target = file(config.goldenMaster.targetPath)

logger.debug "*"*80
logger.debug target.canonicalPath
logger.debug target.exists().toString()
logger.debug "*"*80

if (target.exists()) {
    target.eachFileRecurse { f ->
    logger.info f.name
        if (f.name == 'src') {
	        //copy build file
            def parentFilePath = f.parentFile.path
            def language = parentFilePath.split('[/\\\\]')[-3]
            def docFormat = parentFilePath.split('[/\\\\]')[-1]
            new File(parentFilePath +"/build.gradle")
                .write(
                    new File("subBuild.gradle").text
                    .replaceAll('%LANG%', language)
                    .replaceAll('%TYPE%', docFormat)
                )
            def projectIdentifier = "${language}:${docFormat}"
            logger.info "include subproject ${projectIdentifier} in ${parentFilePath}"
            include(projectIdentifier)
            project(":${projectIdentifier}").projectDir = new File(parentFilePath)
        }
    }
}
include 'publish'
